prototype(Arsors.GlobalContent:Abstract) < prototype(Neos.Neos:Page) {
    head {
        stylesheets = Neos.Fusion:Component {
            style = Neos.Fusion:ResourceUri {
                path = 'resource://Arsors.GlobalContent/Public/Styles/style.css'
            }
            renderer = afx`
                <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.13.1/css/all.min.css" />
                <link rel="stylesheet" href={props.style} />
            `
        }
    }

    body = Neos.Fusion:Component {

        javascripts = Neos.Neos:ContentComponent {
            jquery = 'https://code.jquery.com/jquery-3.5.1.min.js'
            src = Neos.Fusion:ResourceUri {
                path = 'resource://Arsors.GlobalContent/Public/JavaScripts/script.js'
            }
            renderer = afx`
                <script src={props.jquery}></script>
                <script src={props.src}></script>
            `
        }

        @context.list = ${Arsors.GlobalContent.generateGlobalContentArray(node.nodeType.localConfiguration.ui.groups,node.nodeType.localConfiguration.properties)}
        generateEditableByNodeProperties = Neos.Fusion:Loop {
            items = ${list}
            itemKey = 'groupKey'
            itemName = 'groupValue'
            itemRenderer = Neos.Fusion:Component {
                editableNodes = Neos.Fusion:Loop {
                    items = ${groupValue.properties}
                    itemKey = 'propKey'
                    itemName = 'propValue'
                    itemRenderer = Neos.Neos:ContentComponent {
                        renderer = afx`
                            <fieldset>
                                <legend>{propKey}</legend>
                                <Neos.Neos:Editable property={propKey} />
                            </fieldset>
                        `
                    }
                }
                renderer = afx`
                    <div class="accordion-wrapper">
                        <div class="accordion-label" hc-href={groupKey} hc-active={!groupValue.collapsed ? 'true' : NULL}>
                        <i class={"fas fa-"+groupValue.icon}></i>
                            {groupValue.label}
                            <svg class="accordion-arrow" viewBox="0 0 512 512"><path fill="currentColor" d="M8 256C8 119 119 8 256 8s248 111 248 248-111 248-248 248S8 393 8 256zm231-113.9L103.5 277.6c-9.4 9.4-9.4 24.6 0 33.9l17 17c9.4 9.4 24.6 9.4 33.9 0L256 226.9l101.6 101.6c9.4 9.4 24.6 9.4 33.9 0l17-17c9.4-9.4 9.4-24.6 0-33.9L273 142.1c-9.4-9.4-24.6-9.4-34 0z"></path></svg>
                        </div>
                        <div class="accordion-content" hc-id={groupKey}>
                            {props.editableNodes}
                        </div>
                    </div>
                `
            }
        }

        label = ${node.nodeType.localConfiguration.ui.label ? node.nodeType.localConfiguration.ui.label : 'Global Content' }
        icon = ${node.nodeType.localConfiguration.ui.icon ? node.nodeType.localConfiguration.ui.icon : 'file' }

        # Render
        renderer = afx`
            <div @if.inBackend={node.context.inBackend}>
                <h3>
                    <i class={"fas fa-"+props.icon}></i> {props.label}
                </h3>
                <div hc-wrapper>
                    {props.generateEditableByNodeProperties}
                </div>
            </div>
            <div @if.inFrontend={!node.context.inBackend}></div>
        `
    }
}
# Register Arsors.GlobalContent:GlobalContent
prototype(Arsors.GlobalContent:GlobalContent) < prototype(Arsors.GlobalContent:Abstract)

prototype(getGlobalContent) < prototype(Neos.Fusion:Value) {
    key = null

    # Build instanceof string
    instanceof = Neos.Fusion:Loop {
        items = ${Configuration.setting('Arsors.GlobalContent.instanceof')}
        itemRenderer = ${'[instanceof ' + item +']'}
        @glue = ','
    }

    # Find global content by key
    globalContent = ${q(site).children(this.instanceof).filter('['+this.key+']').property(this.key)}

    # Return
    value = ${this.key ? (this.globalContent) ? this.globalContent : 'Can\'t find global content with key: "'+this.key+'"' : 'No key is set.'}
}
# Register shorthand for getGlobalContent
prototype(ggc) < prototype(getGlobalContent)
